plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
}

apply from: rootProject.file('git_hooks/install_git_hooks.gradle')

android {
    compileSdk target_sdk_version

    defaultConfig {
        applicationId "ru.mobileup.template"
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode 1 // Can use ci.gradle to set versionCode
        versionName "0.0.1"
    }

    signingConfigs {
        debug {

        }
        release {

        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            dimension "environment"
            buildConfigField "String", "BACKEND_URL", '"https://pokeapi.co/"'
        }

        prod {
            dimension "environment"
            buildConfigField "String", "BACKEND_URL", '"https://pokeapi.co/"'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.composeCompiler.get()
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {

    coreLibraryDesugaring libs.android.desugar

    // Modules
    implementation project(':core')
    implementation project(':features')

    // UI
    implementation libs.activity.compose
    implementation libs.splashscreen

    // Architecture
    implementation libs.bundles.decompose
    implementation libs.bundles.replica

    // DI
    implementation libs.koin

    // Debugging
    implementation libs.timber
    debugImplementation libs.bundles.hyperion

    // Testing
    testImplementation libs.junit
    testImplementation libs.extJunit
    testImplementation libs.kotlinTest
    testImplementation libs.mockWebServer
    testImplementation libs.awaitility
    testImplementation libs.robolectric
}
