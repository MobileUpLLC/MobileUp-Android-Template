plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

android {
    compileSdk target_sdk_version

    defaultConfig {
        applicationId "ru.mobileup.template"
        minSdk min_sdk_version
        targetSdk target_sdk_version
        versionCode 1 // Can use ci.gradle to set versionCode
        versionName "0.0.1"
    }

    signingConfigs {
        debug {

        }
        release {

        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-debug"
            applicationIdSuffix ".debug"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions "environment"
    productFlavors {
        dev {
            dimension "environment"
            buildConfigField "String", "BACKEND_URL", '"https://pokeapi.co/"'
        }

        prod {
            dimension "environment"
            buildConfigField "String", "BACKEND_URL", '"https://pokeapi.co/"'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion libs.versions.compose.get()
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }
}

dependencies {
    // Modules
    implementation project(':core')
    implementation project(':features')

    coreLibraryDesugaring libs.android.desugar

    // Kotlin
    implementation libs.kotlin.datetime
    implementation libs.coroutines.android

    // UI
    implementation libs.compose.ui
    implementation libs.compose.material
    implementation libs.activity.compose
    debugImplementation libs.compose.tooling

    // Architecture
    implementation libs.sesame.localizedString
    implementation libs.bundles.decompose
    debugImplementation libs.replica.devtools.debug
    releaseImplementation libs.replica.devtools.release

    // DI
    implementation libs.koin.android

    // Network
    implementation libs.retrofit.core

    // Serialization
    implementation libs.serialization.json

    // Debugging
    implementation libs.timber
    debugImplementation libs.bundles.hyperion

    // Testing
    testImplementation libs.junit
    testImplementation libs.extJunit
    testImplementation libs.bundles.koin.test
    testImplementation libs.mockWebServer
    testImplementation libs.awaitility
    testImplementation libs.robolectric
}